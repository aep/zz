using <stdio.h>::{printf};
using <stdlib.h>::{atoi};
using <buffer.h>::{strcmp, memset};
using err;
using buffer;
using json;

enum Fuel {
    Dinosaurs,
    Electric,
}

struct Engine{
    Fuel fuel;
    int charge[8];
}

struct Vehicle {
    int height;
    int wheels;
    Engine engine;
}

symbol InvalidValue;

fn deserialize_charge(json::U *u, err::Err+et mut *e, json::Parser+pt mut* p, char *k, json::Value v)
    where err::checked(*e)
{
    printf("charge.>%s< == >%s< %u, %d [%zu]\n", k, v.string, v.t, v.integer, v.index);
}

fn deserialize_engine(json::U *u, err::Err+et mut *e, json::Parser+pt mut* p, char *k, json::Value v)
    where err::checked(*e)
    where nullterm(k)
    where nullterm(v.string)
{
    let into = (Engine mut*)u->user1;
    static_attest(safe(into));
    printf("engine.>%s< == >%s< %d\n", k, v.string, v.integer);
    if buffer::cstr_eq("fuel", k) && v.t == json::ValueType::String{
        if buffer::cstr_eq("dinosaurs", v.string) {
            into->fuel = Fuel::Dinosaurs;
        } else if buffer::cstr_eq("electric", v.string)  {
            into->fuel = Fuel::Electric;
        } else {
            err::fail(e, InvalidValue , "invalid fuel value %s", v);
        }
    } else if buffer::cstr_eq("charge", k) && v.t == json::ValueType::Array {
        json::next(p, e, json::U{
            it: deserialize_charge,
            user1: &(into->charge)
        });
        if err::check(e) {
            return;
        }
    }
}

fn deserialize_vehicle(json::U *u, err::Err+et mut *e, json::Parser+pt mut* p, char *k, json::Value v)
    where err::checked(*e)
    where nullterm(k)
{
    let into = (Vehicle mut*)u->user1;
    static_attest(safe(into));
    printf("vehicle.>%s< == >%s< %d\n", k, v.string, v.integer);

    if buffer::cstr_eq("height", k) {
        into->height = v.integer;
    } else if buffer::cstr_eq("engine", k) {
        json::next(p, e, json::U{
            it:     deserialize_engine,
            user1:  &(into->engine),
        });
        if err::check(e) {
            return;
        }
    }
}



export fn main() -> int {
    err::Err+1000 mut e = {0};
    err::make(&e);


    Vehicle mut v = {0};

    new+100 parser = json::parser(&e, json::U{ it: deserialize_vehicle, user1: (void mut*)&v});
    err::abort(&e);
    char *str = r#"{
        "height":   
            12,
        "add": "\"[{123123}:b",
        "v": [23],
        "engine": {
            "rolling": "hard",
            "fuel": "electric",
            "charge": [9229, -399888]
        },
        "deep": "nah"
    }"#;

    json::push(&parser, &e, str, static(len(str)));
    err::abort(&e);

    return 0;
}
