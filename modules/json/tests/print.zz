using <stdio.h>::{printf, fread, stdin, stderr};
using <stdlib.h>::{atoi};
using <buffer.h>::{strcmp, memset};
using err;
using buffer;
using json;



test backspace {
    stdin = r#"{"developers": "C:\\developers\\developers"}"#
    stdout = "developers : C:\\developers\\developers\n"
}


test test123  {
    stdin = r#"{
        "height":      
            12,
        "add": "\"[{123123}\\:b",
        "v": [23],
        "engine": {
            "rolling": "hard",
            "fuel": "electric",
            "charge": [9229, 1, -399888]
        },
        "deep": "nah"
    }"#
    stdout = r#"height : 12
add : "[{123123}\:b
v:
  0 : 23
engine:
  rolling : hard
  fuel : electric
  charge:
    0 : 9229
    1 : 1
    2 : -399888
deep : nah
"#
}

test test_uint {
    stdin   = "{\"a\":19}"
    stdout  = "a : 19\n"
}

test test_sint {
    stdin   = "{\"a\":-19}"
    stdout  = "a : -19\n"
}

test test_escape {
    stdin   = r#"{"a": "\"yo\no\""}"#
    stdout  = r#"a : "yo
o"
"#
}

test test_array {
    stdin   = r#"{"a": [
    -3000, 1,
    "yep"
]}"#
    stdout  = r#"a:
  0 : -3000
  1 : 1
  2 : yep
"#
}


test _object_in_array {
    stdin = r#"{"args":[{"LiteralString":{"loc":{"file":"/home/aep/proj/zz/tests/mustpass/procmacro/src/main.zz"},"v":"world"}},{"Literal":{"loc":{"file":"/home/aep/proj/zz/tests/mustpass/procmacro/src/main.zz"},"v":"32"}}]}"#
    stdout = r#"args:
  0:
    LiteralString:
      loc:
        file : /home/aep/proj/zz/tests/mustpass/procmacro/src/main.zz
      v : world
  1:
    Literal:
      loc:
        file : /home/aep/proj/zz/tests/mustpass/procmacro/src/main.zz
      v : 32
"#
}



fn pretty(err::Err+et mut *e, json::Parser+pt mut* p, void mut * user, char *k, json::Value v)
    where err::checked(*e)
{
    let depth = (usize mut*)user;

    for (usize mut i = 0; i < (usize)depth * 2; i++) {
        printf(" ");
    }

    switch v.t {
        json::ValueType::Object => {
            printf("%s:\n", k);
            json::next(p,  e, pretty, (void mut*)((usize)depth + 1));
            if err::check(e) {
                return;
            }
        }
        json::ValueType::Array => {
            printf("%s:\n", k);
            json::next(p,  e, pretty, (void mut*)((usize)depth + 1));
            if err::check(e) {
                return;
            }
        }
        json::ValueType::String => {
            printf("%s : %s\n", k, v.buffer);
        }
        json::ValueType::Integer => {
            printf("%s : %d\n", k, v.integer);
        }
    }
}



export fn main() -> int {
    new+1000 e = err::make();

    usize depth = 0;

    new+100 parser = json::parser(&e, pretty, (void mut*)depth);

    if err::check(&e) {
        err::eprintf(&e, stderr);
        return 2;
    }
    static_attest(err::checked(e));


    char mut buf[10];

    for(;;) {
        usize bin = as<usize>(fread(buf, sizeof(char), static(len(buf)), stdin));
        if bin == 0 {
            break;
        }
        static_attest(bin < len(buf));
        json::push(&parser, &e, buf, bin);
        if err::check(&e) {
            err::eprintf(&e, stderr);
            return 2;
        }
    }


    return 0;
}
